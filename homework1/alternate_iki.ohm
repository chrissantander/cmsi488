Iki {
  Program     =  Block
  Block       =  (Stmt ";")+
  Stmt        =  Type id ("," id)*               -- decl
              |  VarExp "<-" Exp                 -- assignment
              |  "STDIN" "->" VarExp ("," VarExp)*     -- read
              |  "STDOUT" "<-" Exp ("," Exp)*          -- write
              |  "while" Exp "{" Block "}"       -- while
  Type        =  "int" | "bool"
  Exp         =  Exp "or" Exp1                   -- binary
              |  Exp1
  Exp1        =  Exp1 "and" Exp2                 -- binary
              |  Exp2
  Exp2        =  Exp3 relop Exp3                 -- binary
              |  Exp3
  Exp3        =  Exp3 addop Exp4                 -- binary
              |  Exp4
  Exp4        =  Exp4 mulop Exp5                 -- binary
              |  Exp5
  Exp5        =  prefixop Exp6                   -- unary
              |  Exp6
  Exp6        =  boollit
              |  intlit
              |  VarExp
              |  "(" Exp ")"                     -- parens
  VarExp      = id

  keyword     =  ("var" | "read" | "write" | "while" | "loop"
              |  "end" | "int" | "bool" | "true" | "false") ~idrest
  id          =  ~keyword letter idrest*
  idrest      =  "_" | alnum
  intlit      =  digit+
  boollit     =  "true" | "false"
  addop       =  "+" | "-"
  relop       =  "<=" | "<" | "==" | "!=" | ">=" | ">"
  mulop       =  "*" | "/" | "%"
  prefixop    =  ~"--" "-" | "not"

  space      +=  comment
  comment     =  "--" (~"\n" any)* "\n"
}
